import os.path as path
from smod import DoxyVersionParser
from smod import ProgramVersion

Import("build_env")


#set here some directories
api_doc_dir = "api-doc"
api_doc_html_dir = "html"


doxygen_version = DoxyVersionParser().parse()
doxygen_dict = {"@LIBPREFIX@":"${LIBPREFIX}","@LIBNAME@":"${LIBNAME}",
                "@VERSION@":"${VERSION}"}
doxygen_mainpage = build_env.Substfile(["mainpage.in"],SUBST_DICT=doxygen_dict)
doxyfile_source = ["Doxyfile.standard.in"]
if doxygen_version > ProgramVersion(1,6,0):
    doxyfile_source.extend(["Doxyfile.extension.in"])

doxyfile = build_env.Substfile(target="Doxyfile",source= doxyfile_source,
                               SUBST_DICT=doxygen_dict,SUBSTFILESUFFIX="")             
            
doxygen_api_doc = build_env.Command(["api-doc/html"],doxyfile,
                                    "cd doc; doxygen $SOURCE.file")
build_env.Depends(doxygen_api_doc,[doxygen_mainpage])
build_env.Depends(doxygen_api_doc,Glob('../src/*.hpp'))
build_env.Depends(doxygen_api_doc,Glob('../src/h5/*.hpp'))

Clean(doxygen_api_doc,"api-doc")
admin_guide = SConscript("admin-guide/SConscript")
users_guide = SConscript("users-guide/SConscript")



build_env.Alias("doc",[doxygen_api_doc,admin_guide,users_guide])
     
api_html_doc_install = build_env.Install("${DOCDIR}/api","api-doc/html")
admin_guide_install = build_env.Install("${DOCDIR}",admin_guide)
users_guide_install = build_env.Install("${DOCDIR}",users_guide)

build_env.Alias("all",[doxygen_api_doc,admin_guide,users_guide])
build_env.Alias("install",[api_html_doc_install,admin_guide_install,
                           users_guide_install])


